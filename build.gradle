plugins {
    id 'org.springframework.boot' version '4.0.1' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.sonarqube' version '7.2.2.6593'
    id 'com.diffplug.spotless' version '6.25.0'
}

ext {
    springdocVersion = '3.0.0'
    querydslVersion = '5.1.0'
    nimbusJoseJwtVersion = '10.6'
}

allprojects {
    group = 'io.mopl'
    version = '1.0.0'
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'com.diffplug.spotless'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    spotless {
        java {
            googleJavaFormat()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:4.0.1'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
        }
    }
}

sonar {
    properties {
        property "sonar.projectKey", System.getenv("SONAR_PROJECT_KEY")
        property "sonar.organization", System.getenv("SONAR_ORGANIZATION")
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

tasks.register('installGitHooks', Copy) {
    from new File(rootProject.rootDir, 'scripts/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    
    doLast {
        def hookFile = new File(rootProject.rootDir, '.git/hooks/pre-commit')
        hookFile.setExecutable(true)
    }
}

// Ensure hooks are installed when building the project
tasks.named('build') {
    dependsOn('installGitHooks')
}